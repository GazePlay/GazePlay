plugins {
    id 'application'
    id 'distribution'
    id 'jacoco'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'io.freefair.lombok' version '4.1.4'
    id 'com.github.hierynomus.license-report' version '0.15.0'
    id 'com.github.spotbugs' version '2.0.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'org.beryx.runtime' version '1.3.0'

    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

// Add -PSpotBugs=false to the Gradle Command Line to not run spotbugs
boolean spotbugsEnabled = Boolean.parseBoolean(project.getProperties().get('SpotBugs')) ?: true

// Add -Ppmd=false to the Gradle Command Line to not run PMD
boolean pmdEnabled = Boolean.parseBoolean(project.getProperties().get('pmd')) ?: true

ext.javafxVersion = "11.0.2"

apply from: "${rootDir}/gradle/package.gradle"
apply from: "${rootDir}/gradle/installer.gradle"
apply from: "${rootDir}/gazeplay-dist/build.gradle"

allprojects {
    group = 'com.github.schwabdidier'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven {
            url = 'https://jitpack.io'
        }

        maven {
            url = 'https://raw.github.com/agomezmoron/screen-recorder/mvn-repo'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'org.openjfx.javafxplugin'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.hierynomus.license-report'
    apply plugin: 'com.github.spotbugs'
    apply plugin: "pmd"
    apply plugin: 'distribution'

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    apply from: "${rootDir}/gradle/integration.gradle"

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
    }

    javafx {
        version = javafxVersion
        modules = ['javafx.controls', 'javafx.swing', 'javafx.media', 'javafx.web']
    }

    spotbugs {
        toolVersion = '4.0.0-beta3'

        //ignoreFailures = false
        //effort = "max"
        //reportLevel = "high"
        excludeFilter = file("spotbugs-exclude.xml")
    }

    spotbugsMain.enabled = spotbugsEnabled
    spotbugsTest.enabled = spotbugsEnabled

    downloadLicenses {
        dependencyConfiguration = 'compileClasspath'
        includeProjectDependencies = true
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
        testImplementation "org.testfx:testfx-core:4.0.16-alpha"
        testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
        testImplementation "org.testfx:openjfx-monocle:jdk-11+26"
    }

    sourceCompatibility = '11'

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
        if (project.hasProperty('excludeTests')) {
            exclude project.property('excludeTests')
        }
    }

    jacoco {
        toolVersion '0.8.3' // We need this version for JMockit to work.
    }

    tasks.jacocoTestReport.dependsOn('test')

    pmd {
        ignoreFailures = true
        rulePriority = 2
        sourceSets = [sourceSets.main, sourceSets.test]
        reportsDir = file("$project.buildDir/reports/pmd")
        ruleSets = []
        ruleSetFiles = files(rootProject.file("pmd-ruleset.xml"))
    }

    pmdMain.enabled = pmdEnabled
    pmdTest.enabled = pmdEnabled

    task checkPMDReport {
        doLast {
            def pmdReportDir = file("$project.buildDir/reports/pmd/")
            if (pmdReportDir.exists()) {
                def pmdReportXml = file("$project.buildDir/reports/pmd/main.xml")
                if (pmdReportXml.exists()) {
                    def pmdReportHtml = file("$project.buildDir/reports/pmd/main.html")
                    def rootNode = new XmlSlurper().parse("$pmdReportXml")
                    // thresholds are set from actual observations of the number of violation
                    // goal is that no new violations come in the code base
                    // as we progressively fix existing violations (or exclude them in pmd-ruleset.xml), we should lower the threshold accordingly
                    // until the threshold is zero
                    def violationsThresholds = [
                            1: 40,
                            2: 6,
                            3: 5201,
                            4: 197,
                            5: 449
                    ]
                    violationsThresholds.each {
                        priorityLevel, violationsThreshold ->

                            def violationsCount = rootNode.children().children().findAll({ node -> node.name() == 'violation' && node.@priority == priorityLevel }).size()
                            def violationsMessage = "$violationsCount PMD rule 'priority $priorityLevel' violations were found. See the report at: file://$pmdReportHtml" 
                            if (violationsCount > violationsThreshold) {
                                throw new GradleException(violationsMessage)
                            } else {
                                if (violationsCount > 0) {
                                    println violationsMessage
                                }
                            }
                    }
                }
            }
        }
    }

}

dependencies {
    runtime project(':gazeplay')

    runtime project(':gazeplay-games')
    runtime project(':gazeplay-games-commons')
    runtime project(':gazeplay-commons')
    runtime project(':gazeplay-melordi')
    runtime project(':gazeplay-picto-pick')

    implementation 'javax.servlet:javax.servlet-api:4.0.1'
}

distributions {
    main {
        contents {
            from("build/reports")
            from("build/scripts") {
                into 'bin'
            }
            from("gazeplay-dist/src/jre") {
                into 'lib/jre'
            }
        }
    }
}

distTar {
    enabled = true
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}
distZip {
    duplicatesStrategy = 'exclude'
}
[bootJar, bootDistTar, bootDistZip]*.enabled = false

runtime {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    modules = jreModules
    jreDir = file("gazeplay-dist/src/jre")
}

task cleanJre(type: Delete) {
    delete 'gazeplay-dist/src/jre'
}

release {
    failOnPublishNeeded = false
    failOnCommitNeeded = false
    versionPropertyFile = 'gradle.properties'
}

mainClassName = 'net.gazeplay.GazePlayLauncher'

defaultTasks 'clean', 'build', 'checkPMDReport'
startScripts.enabled = false

afterReleaseBuild.dependsOn generateInstaller
