pmdMain.group = 'verification'
pmdTest.group = 'verification'

def gazeplayViolationThresholds = [
        1: 0,
        2: 0,
        3: 1895,
        4: 22,
        5: 132
]

def gazeplayCommonsViolationThresholds = [
        1: 0,
        2: 0,
        3: 1011,
        4: 35,
        5: 74
]

def gazeplayGamesViolationThresholds = [
        1: 37,
        2: 2,
        3: 5079,
        4: 197,
        5: 438
]

def gazeplayGamesCommonsViolationThresholds = [
        1: 0,
        2: 0,
        3: 122,
        4: 6,
        5: 21
]

task checkPMDReport(dependsOn: 'pmdMain', group: 'verification') {
    doLast {
        def pmdReportXml = file("$project.buildDir/reports/pmd/main.xml")
        if (pmdReportXml.exists()) {
            def pmdReportHtml = file("$project.buildDir/reports/pmd/main.html")
            def rootNode = new XmlSlurper().parse("$pmdReportXml")
            // thresholds are set from actual observations of the number of violation
            // goal is that no new violations come in the code base
            // as we progressively fix existing violations (or exclude them in pmd-ruleset.xml), we should lower the threshold accordingly
            // until the threshold is zero
            def violationsThresholds

            switch(project.name) {
                case 'gazeplay':
                    violationsThresholds = gazeplayViolationThresholds
                    break
                case 'gazeplay-commons':
                    violationsThresholds = gazeplayCommonsViolationThresholds
                    break
                case 'gazeplay-games':
                    violationsThresholds = gazeplayGamesViolationThresholds
                    break
                case 'gazeplay-games-commons':
                    violationsThresholds = gazeplayGamesCommonsViolationThresholds
                    break
                default:
                    violationsThresholds = [
                            1: 0,
                            2: 0,
                            3: 0,
                            4: 0,
                            5: 0
                    ]
            }

            violationsThresholds.each {
                priorityLevel, violationsThreshold ->

                    def violationsCount = rootNode.children().children().findAll({ node -> node.name() == 'violation' && node.@priority == priorityLevel }).size()
                    def violationsMessage = "$violationsCount PMD rule 'priority $priorityLevel' violations were found. See the report at: file://$pmdReportHtml"
                    if (violationsCount > violationsThreshold) {
                        throw new GradleException(violationsMessage)
                    } else {
                        if (violationsCount > 0) {
                            println violationsMessage
                        }
                    }
            }
        }
    }
}
