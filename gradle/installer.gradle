task generateInstaller(dependsOn: ['unzipDistribution']) {
    doLast {
        def innoSetupDir = new File("${buildDir}/innosetup")

        innoSetupDir.mkdir()

        copy {
            from("${rootDir}/gradle/innosetup/setup.iss.skel")
            rename("setup.iss.skel", "setup.iss")
            expand([
                    applicationVersion: "${version}",
                    unpackedDirectory : "gazeplay-project-${version}"
            ])
            into(innoSetupDir)
        }

        copy {
            from("${rootDir}/gazeplay/src/main/resources/data/common/licence.txt")
            expand([
                    "version": project.version,
                    "date"   : new Date().format('yyyy-MM-dd')
            ])
            into(innoSetupDir)
        }

        copy {
            from("${rootDir}/gradle/innosetup/gazeplayicon.ico")
            into(innoSetupDir)
        }

        copy {
            from("${rootDir}/gradle/innosetup/gazeplayicon.bmp")
            into(innoSetupDir)
        }

        // I'm leaving this line in so we can quickly sign installers if/when we decide to do so.
        // def signtool = "/SStandard=\"C:\\Program Files (x86)\\Windows Kits\\10\\bin\\10.0.18362.0\\x64\\signtool.exe sign /a /n \$qGazePlay\$q /tr http://timestamp.comodoca.com/ /d \$qGazePlay\$q \$f\""

        exec {
            workingDir rootDir
            commandLine "C:\\Program Files (x86)\\Inno Setup 6\\ISCC.exe", "${innoSetupDir}\\setup.iss"
        }
    }
}

task unzipDistribution(dependsOn: ['packageApp'], type: Copy) {
    from zipTree("${buildDir}/distributions/gazeplay-project-${version}.zip")
    into buildDir
}
